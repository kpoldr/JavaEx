plugins {

	id 'io.spring.dependency-management' version '1.1.0'
	id "com.github.node-gradle.node" version "3.5.0"
	id "org.gretty" version "4.0.3"
	id 'java'
}

sourceCompatibility = 17

apply plugin: 'java'

compileJava {
	options.compilerArgs << '-parameters'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

gretty {
	contextPath = '/'
}

repositories {
	mavenCentral()
}

dependencies {

	implementation 'org.springframework:spring-webmvc:6.0.2'
	implementation 'org.springframework:spring-jdbc:6.0.2'
	implementation 'org.springframework:spring-aop:6.0.2'
	implementation 'org.springframework:spring-context:6.0.2'
	implementation 'org.apache.commons:commons-lang3:3.12.0'
	implementation 'com.fasterxml.jackson.core:jackson-databind:2.14.1'
	compileOnly 'javax.servlet:javax.servlet-api:4.0.1'
	runtimeOnly 'org.aspectj:aspectjweaver:1.9.9.1'
	compileOnly 'org.projectlombok:lombok:1.18.24'
	annotationProcessor 'org.projectlombok:lombok:1.18.24'
	runtimeOnly 'org.hsqldb:hsqldb:2.7.1'
	runtimeOnly 'org.postgresql:postgresql:42.5.1'

	implementation 'org.hibernate.validator:hibernate-validator:8.0.0.Final'

	testImplementation 'org.springframework.boot:spring-boot-starter-test:3.0.0'
}

tasks.named('test') {
	useJUnitPlatform()
}

node {

	// If false, it will use the globally installed Node.js
	// If true, it will download node using above parameters
	download = true

	// Version of node to download and install (only used if download is true)
	// It will be unpacked in the workDir
	version = "16.14.0"

	// The directory where Node.js is unpacked (when download is true)
	workDir = file("${project.projectDir}/react/nodejs")

	// The directory where npm is installed (when a specific version is defined)
	npmWorkDir = file("${project.projectDir}/react/npm")

	// The directory of the react project
	nodeProjectDir = file("${project.projectDir}/react")

}

task buildReactApp(type: NodeTask, dependsOn: 'npmInstall') {

	script = project.file('/react/node_modules/webpack/bin/webpack.js')
	args = [
			'--mode', 'development',
			'--entry', './src/index.tsx',
			'-o', '../src/main/webapp/js'
	]
}


processResources.dependsOn 'buildReactApp'

clean.delete << file('src/node_modules')
clean.delete << file('src/main/webapp/js')
